@PACKAGE_INIT@

#MexIFaceConfig.cmake

include(CMakeFindDependencyMacro)
find_dependency(BacktraceException)
find_dependency(Armadillo)
add_definitions(-DARMA_USE_CXX11)


#Sets:
# MexIFace_BUILD_TYPES
# MexIFace_CMAKE_DIR
# MexIFace_CMAKE_MODULES_DIR
# MexIFace_CMAKE_TOOLCHAINS_DIR
# MexIFace_CMAKE_FUNCTIONS_DIR
# MexIFace_CMAKE_EXECUTABLE_DIR
# MexIFace_CMAKE_TEMPLATES_DIR
#
# MexIFace_TARGETS
# MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS
# MexIFace_COMPATABLE_MATLAB_VERSIONS
# MexIFace_COMPATABLE_MATLAB_RELEASES
# MexIFace_COMPATABLE_MATLAB_ARCH
# MexIFace_MATLAB_INTERLEAVED_COMPLEX - Settings for Matlab complex API
# MexIFace_MATLAB_LARGE_ARRAY_DIMS - Settings for Matlab 64-bit index API
#
# MexIFace_SYSTEM_LIBSTDCXX_VERSION

# Modifies:
#  CMAKE_MODULE_PATH


list(APPEND CMAKE_MODULE_PATH ${MexIFace_CMAKE_MODULES_DIR} )

#Find all matlab dependencies
set(MexIFace_TARGETS @MexIFace_TARGETS@)
set(MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS @MexIFace_MATLAB_VERSION_STRINGS@)
set(MexIFace_COMPATABLE_MATLAB_VERSIONS @MexIFace_MATLAB_VERSIONS@)
set(MexIFace_COMPATABLE_MATLAB_RELEASES @MexIFace_MATLAB_RELEASES@)
set(MexIFace_COMPATABLE_MATLAB_ARCH @MexIFace_MATLAB_SYSTEM_ARCH@)
set(MexIFace_MATLAB_INTERLEAVED_COMPLEX @MexIFace_MATLAB_INTERLEAVED_COMPLEX@) #Propagate settings for complex API
set(MexIFace_MATLAB_LARGE_ARRAY_DIMS @MexIFace_MATLAB_LARGE_ARRAY_DIMS@) #Propagate settings for array index types API

# @PACKAGE_NAME@Config-mexiface.cmake Sets properties that are particular to the Build vs. Install tree
#MexIFace_MATLAB_CODE_DIR
#MexIFace_MATLAB_STARTUP_M
set_and_check(_MexIFace_CONFIG_FILE ${CMAKE_CURRENT_LIST_DIR}/@PACKAGE_NAME@Config-mexiface.cmake)
include(${_MexIFace_CONFIG_FILE})
unset(_MexIFace_CONFIG_FILE)
set_and_check(MexIFace_CMAKE_DIR @PACKAGE_SHARED_CMAKE_DIR@)
set_and_check(MexIFace_CMAKE_MODULES_DIR ${MexIFace_CMAKE_DIR}/Modules)
set_and_check(MexIFace_CMAKE_TOOLCHAINS_DIR ${MexIFace_CMAKE_DIR}/Toolchains)
set_and_check(MexIFace_CMAKE_FUNCTIONS_DIR ${MexIFace_CMAKE_DIR}/Functions)
set_and_check(MexIFace_CMAKE_EXECUTABLE_DIR ${MexIFace_CMAKE_DIR}/Executables)
set_and_check(MexIFace_CMAKE_TEMPLATES_DIR ${MexIFace_CMAKE_DIR}/Templates)

if(CMAKE_CXX_COMPILER_ID STREQUAL GNU AND NOT MexIFace_SYSTEM_LIBSTDCXX_VERSION)
    set(MexIFace_SYSTEM_LIBSTDCXX_VERSION @MexIFace_SYSTEM_LIBSTDCXX_VERSION@)
endif()
include(MexIFace-configure-matlab)

#These functions are used by clients to configure their own mex modules with MexIFace
include(${MexIFace_CMAKE_FUNCTIONS_DIR}/mexiface_make_mex.cmake)
include(${MexIFace_CMAKE_FUNCTIONS_DIR}/mexiface_configure_install.cmake)

#Include targets file.  This will create IMPORTED target MexIFace::MexIFaceX_Y for each version of matlab.
include(${CMAKE_CURRENT_LIST_DIR}/@EXPORT_TARGETS_NAME@.cmake)
list(GET MexIFace_TARGETS 0 _target)
get_property(MexIFace_BUILD_TYPES TARGET MexIFace::${_target} PROPERTY IMPORTED_CONFIGURATIONS)
unset(_target)

message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_BUILD_TYPES: ${@PROJECT_NAME@_BUILD_TYPES}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_DIR: ${@PROJECT_NAME@_CMAKE_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_MODULES_DIR: ${@PROJECT_NAME@_CMAKE_MODULES_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR: ${@PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_EXECUTABLE_DIR: ${@PROJECT_NAME@_CMAKE_EXECUTABLE_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_TEMPLATES_DIR: ${@PROJECT_NAME@_CMAKE_TEMPLATES_DIR}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_TARGETS:${MexIFace_TARGETS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS:${MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_VERSIONS:${MexIFace_COMPATABLE_MATLAB_VERSIONS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_RELEASES:${MexIFace_COMPATABLE_MATLAB_RELEASES}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_ARCH:${MexIFace_COMPATABLE_MATLAB_ARCH}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_MATLAB_INTERLEAVED_COMPLEX:${MexIFace_MATLAB_INTERLEAVED_COMPLEX}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_MATLAB_LARGE_ARRAY_DIMS:${MexIFace_MATLAB_LARGE_ARRAY_DIMS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_SYSTEM_LIBSTDCXX_VERSION:${MexIFace_SYSTEM_LIBSTDCXX_VERSION}")

check_required_components("@PROJECT_NAME@")
