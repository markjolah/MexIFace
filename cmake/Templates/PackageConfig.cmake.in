@PACKAGE_INIT@

#MexIFaceConfig.cmake

include(CMakeFindDependencyMacro)
find_dependency(BacktraceException)
find_dependency(Armadillo)
add_definitions(-DARMA_USE_CXX11)


#Sets:
# @PROJECT_NAME@_BUILD_TYPES
# @PROJECT_NAME@_CMAKE_DIR
# @PROJECT_NAME@_CMAKE_MODULES_DIR
# @PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR
# @PROJECT_NAME@_CMAKE_FUNCTIONS_DIR
# @PROJECT_NAME@_CMAKE_EXECUTABLE_DIR
# @PROJECT_NAME@_CMAKE_TEMPLATES_DIR
#
# MEXIFACE_TARGETS
# MEXIFACE_COMPATABLE_MATLAB_VERSION_STRINGS
# MEXIFACE_COMPATABLE_MATLAB_VERSIONS
# MEXIFACE_COMPATABLE_MATLAB_RELEASES
# MEXIFACE_COMPATABLE_MATLAB_ARCH
# MEXIFACE_MATLAB_INTERLEAVED_COMPLEX - Settings for Matlab complex API
# MEXIFACE_MATLAB_LARGE_ARRAY_DIMS - Settings for Matlab 64-bit index API
#
# Only one of these variables can be set
# MEXIFACE_LIBSTDCXX - Version of LIBSTDCXX this set of Mexiface targets is built against
# MEXIFACE_LIBCXX - Version of LIBCXX his set of Mexiface targets is built against

# Modifies:
#  CMAKE_MODULE_PATH


#Setup paths to CMAKE utilities
set_and_check(@PROJECT_NAME@_CMAKE_DIR @PACKAGE_SHARED_CMAKE_INSTALL_DIR@) #Relative to this file's install location
set_and_check(@PROJECT_NAME@_CMAKE_MODULES_DIR ${@PROJECT_NAME@_CMAKE_DIR}/Modules)
set_and_check(@PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR ${@PROJECT_NAME@_CMAKE_DIR}/Toolchains)
set_and_check(@PROJECT_NAME@_CMAKE_FUNCTIONS_DIR ${@PROJECT_NAME@_CMAKE_DIR}/Functions)
set_and_check(@PROJECT_NAME@_CMAKE_EXECUTABLE_DIR ${@PROJECT_NAME@_CMAKE_DIR}/Executables)
set_and_check(@PROJECT_NAME@_CMAKE_TEMPLATES_DIR ${@PROJECT_NAME@_CMAKE_DIR}/Templates)
list(APPEND CMAKE_MODULE_PATH ${@PROJECT_NAME@_CMAKE_MODULES_DIR} )

#Find all matlab dependencies
set(MEXIFACE_TARGETS @MEXIFACE_TARGETS@)
set(MEXIFACE_COMPATABLE_MATLAB_VERSION_STRINGS @MEXIFACE_MATLAB_VERSION_STRINGS@)
set(MEXIFACE_COMPATABLE_MATLAB_VERSIONS @MEXIFACE_MATLAB_VERSIONS@)
set(MEXIFACE_COMPATABLE_MATLAB_RELEASES @MEXIFACE_MATLAB_RELEASES@)
set(MEXIFACE_COMPATABLE_MATLAB_ARCH @MEXIFACE_MATLAB_SYSTEM_ARCH@)
set_and_check(_MEXIFACE_CONFIG_FILE  ${CMAKE_CURRENT_LIST_DIR}/@PACKAGE_NAME@Config-mexiface.cmake)
include(${_MEXIFACE_CONFIG_FILE}) #Sets MEXIFACE_MATLAB_CODE_DIR and MEXIFACE_MATLAB_STARTUP_M depending on use from build or install dir

if(CMAKE_CXX_COMPILER_ID STREQUAL GNU AND NOT MEXIFACE_SYSTEM_LIBSTDCXX_VERSION)
    set(MEXIFACE_SYSTEM_LIBSTDCXX_VERSION @MEXIFACE_SYSTEM_LIBSTDCXX_VERSION@)
endif()
set(MEXIFACE_MATLAB_INTERLEAVED_COMPLEX @MEXIFACE_MATLAB_INTERLEAVED_COMPLEX@) #Propagate settings for complex API
set(MEXIFACE_MATLAB_LARGE_ARRAY_DIMS @MEXIFACE_MATLAB_LARGE_ARRAY_DIMS@) #Propagate settings for array index types API
include(MexIFace-configure-matlab)

#These functions are used by clients to configure their own mex modules with MexIFace
include(@PROJECT_NAME@_CMAKE_FUNCTIONS_DIR/mexiface_make_mex.cmake)
include(@PROJECT_NAME@_CMAKE_FUNCTIONS_DIR/mexiface_configure_install.cmake)

#Include targets file.  This will create IMPORTED target MexIFace::MexIFaceX_Y for each version of matlab.
include(${CMAKE_CURRENT_LIST_DIR}/@EXPORT_TARGETS_NAME@.cmake)
get_property(@PROJECT_NAME@_BUILD_TYPES TARGET @PROJECT_NAME@::@PROJECT_NAME@ PROPERTY IMPORTED_CONFIGURATIONS)

message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_BUILD_TYPES: ${@PROJECT_NAME@_BUILD_TYPES}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_DIR: ${@PROJECT_NAME@_CMAKE_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_MODULES_DIR: ${@PROJECT_NAME@_CMAKE_MODULES_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR: ${@PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_EXECUTABLE_DIR: ${@PROJECT_NAME@_CMAKE_EXECUTABLE_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_TEMPLATES_DIR: ${@PROJECT_NAME@_CMAKE_TEMPLATES_DIR}")

check_required_components("@PROJECT_NAME@")
