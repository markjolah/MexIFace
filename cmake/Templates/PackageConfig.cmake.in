@PACKAGE_INIT@

#MexIFaceConfig.cmake

include(CMakeFindDependencyMacro)
find_dependency(BacktraceException)

set_and_check(PACKAGE_FIND_MODULES_PATH @PACKAGE_FIND_MODULES_PATH@)
list(INSERT CMAKE_MODULE_PATH 0 ${PACKAGE_FIND_MODULES_PATH})
#Use find_package because find_dependency does not respect COMPONENTS argument or allow
#re-finding of packages with different COMPONENTS settings
find_package(Armadillo REQUIRED COMPONENTS CXX11 INT64) #INT64 always required for MATLAB
list(REMOVE_AT CMAKE_MODULE_PATH 0)

#Sets:
# MexIFace_BUILD_TYPES
# MexIFace_CMAKE_DIR
# MexIFace_CMAKE_MODULES_DIR
# MexIFace_CMAKE_TOOLCHAINS_DIR
# MexIFace_CMAKE_FUNCTIONS_DIR
# MexIFace_CMAKE_EXECUTABLE_DIR
# MexIFace_CMAKE_TEMPLATES_DIR
#
# MexIFace_TARGETS
# MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS
# MexIFace_COMPATABLE_MATLAB_VERSIONS
# MexIFace_COMPATABLE_MATLAB_RELEASES
# MexIFace_COMPATABLE_MATLAB_ARCH
# OPT_MexIFace_MATLAB_INTERLEAVED_COMPLEX - Settings for Matlab complex API
# OPT_MexIFace_MATLAB_LARGE_ARRAY_DIMS - Settings for Matlab 64-bit index API
#
# MexIFace_SYSTEM_LIBSTDCXX_VERSION
#
# Provides functions:
# mexiface_make_mex()
# mexiface_configure_install()


list(APPEND CMAKE_MODULE_PATH ${MexIFace_CMAKE_MODULES_DIR} )

#Find all matlab dependencies
set(MexIFace_TARGETS "@MexIFace_TARGETS@")
set(MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS "@MexIFace_MATLAB_VERSION_STRINGS@")
set(MexIFace_COMPATABLE_MATLAB_VERSIONS "@MexIFace_MATLAB_VERSIONS@")
set(MexIFace_COMPATABLE_MATLAB_RELEASES "@MexIFace_MATLAB_RELEASES@")
set(MexIFace_COMPATABLE_MATLAB_ARCH @MexIFace_MATLAB_SYSTEM_ARCH@)
option(OPT_MexIFace_MATLAB_INTERLEAVED_COMPLEX "Enable interleaved complex API in R2018a+" @OPT_MexIFace_MATLAB_INTERLEAVED_COMPLEX@) #Propagate settings for complex API
option(OPT_MexIFace_MATLAB_LARGE_ARRAY_DIMS "Enable 64-bit array indexes in R2017a+" @OPT_MexIFace_MATLAB_LARGE_ARRAY_DIMS@) #Propagate settings for array index types API
option(OPT_MexIFace_INSTALL_DISTRIBUTION_STARTUP "Install an additional copy of startupPackage.m at the INSTALL_PREFIX root in addition to the normal directory.  This makes it easy to distribute as a zip file." Off)
# @PROJECT_NAME@Config-mexiface.cmake Sets properties that are particular to the Build vs. Install tree
#MexIFace_MATLAB_CODE_DIR
#MexIFace_MATLAB_STARTUP_M
set_and_check(_MexIFace_CONFIG_FILE ${CMAKE_CURRENT_LIST_DIR}/@PROJECT_NAME@Config-mexiface.cmake)
include(${_MexIFace_CONFIG_FILE})
unset(_MexIFace_CONFIG_FILE)

#Main cmake dir for MexIFace. For install-tree export should be a relative path.  May be absolute for build-tree export.
set_and_check(MexIFace_CMAKE_DIR @PACKAGE_SHARED_CMAKE_DIR@)
set_and_check(MexIFace_CMAKE_MODULES_DIR ${MexIFace_CMAKE_DIR}/Modules)
set_and_check(MexIFace_CMAKE_TOOLCHAINS_DIR ${MexIFace_CMAKE_DIR}/Toolchains)
set_and_check(MexIFace_CMAKE_FUNCTIONS_DIR ${MexIFace_CMAKE_DIR}/Functions)
set_and_check(MexIFace_CMAKE_EXECUTABLE_DIR ${MexIFace_CMAKE_DIR}/Executables)
set_and_check(MexIFace_CMAKE_TEMPLATES_DIR ${MexIFace_CMAKE_DIR}/Templates)

if(CMAKE_CXX_COMPILER_ID STREQUAL GNU AND NOT MexIFace_SYSTEM_LIBSTDCXX_VERSION)
    set(MexIFace_SYSTEM_LIBSTDCXX_VERSION @MexIFace_SYSTEM_LIBSTDCXX_VERSION@)
endif()
include(${MexIFace_CMAKE_MODULES_DIR}/MexIFace-configure-matlab.cmake)

#These functions are used by clients to configure their own mex modules with MexIFace
include(${MexIFace_CMAKE_FUNCTIONS_DIR}/mexiface_make_mex.cmake)
include(${MexIFace_CMAKE_FUNCTIONS_DIR}/mexiface_configure_install.cmake)

#Include targets file.  This will create IMPORTED target MexIFace::MexIFaceX_Y for each version of matlab.
include(${CMAKE_CURRENT_LIST_DIR}/@EXPORT_TARGETS_NAME@.cmake)
#Use the first target to get the build configurations exported
list(GET MexIFace_TARGETS 0 _target)
get_property(MexIFace_BUILD_TYPES TARGET ${_target} PROPERTY IMPORTED_CONFIGURATIONS)
unset(_target)

message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_BUILD_TYPES: ${@PROJECT_NAME@_BUILD_TYPES}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_DIR: ${@PROJECT_NAME@_CMAKE_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_MODULES_DIR: ${@PROJECT_NAME@_CMAKE_MODULES_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR: ${@PROJECT_NAME@_CMAKE_TOOLCHAINS_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_EXECUTABLE_DIR: ${@PROJECT_NAME@_CMAKE_EXECUTABLE_DIR}")
message(STATUS "[@PROJECT_NAME@Config.cmake] @PROJECT_NAME@_CMAKE_TEMPLATES_DIR: ${@PROJECT_NAME@_CMAKE_TEMPLATES_DIR}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_TARGETS:${MexIFace_TARGETS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS:${MexIFace_COMPATABLE_MATLAB_VERSION_STRINGS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_VERSIONS:${MexIFace_COMPATABLE_MATLAB_VERSIONS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_RELEASES:${MexIFace_COMPATABLE_MATLAB_RELEASES}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_COMPATABLE_MATLAB_ARCH:${MexIFace_COMPATABLE_MATLAB_ARCH}")
message(STATUS "[MexIFaceConfig.cmake] OPT_MexIFace_MATLAB_INTERLEAVED_COMPLEX:${OPT_MexIFace_MATLAB_INTERLEAVED_COMPLEX}")
message(STATUS "[MexIFaceConfig.cmake] OPT_MexIFace_MATLAB_LARGE_ARRAY_DIMS:${OPT_MexIFace_MATLAB_LARGE_ARRAY_DIMS}")
message(STATUS "[MexIFaceConfig.cmake] MexIFace_SYSTEM_LIBSTDCXX_VERSION:${MexIFace_SYSTEM_LIBSTDCXX_VERSION}")

check_required_components("@PROJECT_NAME@")
